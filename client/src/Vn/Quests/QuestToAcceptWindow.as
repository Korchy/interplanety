package Vn.Quests {
//-----------------------------------------------------------------------------------------------------
// Окно с описанием квеста для его принятия на планете
//-----------------------------------------------------------------------------------------------------
	import Vn.Common.SC;
	import Vn.Interface.Window.VnWindowA;
	import Vn.Interface.Text.VnTextScroll;
	import Vn.Text.TextDictionary;
	import Vn.Interplanety;
//-----------------------------------------------------------------------------------------------------
	public class QuestToAcceptWindow extends VnWindowA {
//-----------------------------------------------------------------------------------------------------
// Переменные
//-----------------------------------------------------------------------------------------------------
		private var lQuest:QuestVn;	// Квест
		private var Intro:VnTextScroll;	// Описание
//-----------------------------------------------------------------------------------------------------
// Конструктор
//-----------------------------------------------------------------------------------------------------
		public function QuestToAcceptWindow(QuestToAccept:QuestVn) {
			// Конструктор предка
			super();
			// Конструктор
			SetLocalPosition(200,175);
			Quest = QuestToAccept;
			Name = Quest.Name;
			Caption.Text = TextDictionary.Text(Name);
			Script = "acceptdynamicquest.php";	// Скрипт, который отрабатывает принятие условия
			Intro = new VnTextScroll(WorkSpace.Width,WorkSpace.Height-60); // Высота на 60 пикс меньше (место для кнопки ОК)
			addChild(Intro);
			Intro.TextColor = SC.BLACK;
			Intro.MoveIntoParent(Width05,WorkSpace.Y+Intro.Height05,true);
			Intro.HtmlText = TextDictionary.Text(Quest.Intro);
		}
//-----------------------------------------------------------------------------------------------------
// Деструктор - вызывать самому, автоматически не вызывается
//-----------------------------------------------------------------------------------------------------
		override public function _delete():void {
			// Деструктор
			Quest = null;
			removeChild(Intro);
			Intro._delete();
			Intro = null;
			// Деструктор предка
			super._delete();
		}
//-----------------------------------------------------------------------------------------------------
// Функции
//-----------------------------------------------------------------------------------------------------
		override protected function CheckLocal():Boolean {
			// Локальные проверки
			// Нельзя принять если квест выше по уровню
			if(Quest.Level>Vn.Interplanety.VnUser.Level) return false;
			return true;
		}
//-----------------------------------------------------------------------------------------------------
		override protected function SetCheckingOnServerParameters():void {
			// Установка параметров скрипта для проверки на сервере
			AddServerParameter("QuestId",String(Quest.Id));	// Id квеста
		}
//-----------------------------------------------------------------------------------------------------
// Обработка событий
//-----------------------------------------------------------------------------------------------------
		
//-----------------------------------------------------------------------------------------------------
// Методы get/set для установки значений переменных
//-----------------------------------------------------------------------------------------------------
		public function get Quest():QuestVn {
			return lQuest;
		}
//-----------------------------------------------------------------------------------------------------
		public function set Quest(Value:QuestVn):void {
			lQuest = Value;
		}
//-----------------------------------------------------------------------------------------------------
	}
}