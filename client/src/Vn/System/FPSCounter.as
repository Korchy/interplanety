package Vn.System {
	// Класс счетчик FPS
	// Считается среднее значение FPS в секунду
//-----------------------------------------------------------------------------------------------------
	import flash.utils.getTimer;	// Таймер
	import Vn.Objects.VnObject;
	import Vn.Interface.Text.VnText;
//-----------------------------------------------------------------------------------------------------
	public class FPSCounter extends VnObject {
//-----------------------------------------------------------------------------------------------------
// Переменные
//-----------------------------------------------------------------------------------------------------
		private var LastTick:uint = 0;		// Кол-во мс. на прошлом тике
		private var LastTickRound:uint = 0;	// Кол-во мс. на прошлом тике для округленной FPS
		private var RefreshCount:uint = 0;	// Кол-во обновлений за 1000 мс.
		private var FramesDiff:Number = 0;	// Кол-во мс. прошедшее с последнего тика (дельта прошлого и текущего тиков)
		private var VisualElement:VnText;
//-----------------------------------------------------------------------------------------------------
// Конструктор
//-----------------------------------------------------------------------------------------------------
		public function FPSCounter() {
			super();
			VisualElement = new VnText();
			VisualElement.x = 5;
			VisualElement.y = 5;
			VisualElement.textColor = 0xF8D830;
			addChild(VisualElement);
			VisualElement.Text = "FPS:";
		}
//-----------------------------------------------------------------------------------------------------
// Деструктор - вызывать самому, автоматически не вызывается
//-----------------------------------------------------------------------------------------------------
		override public function _delete():void {
			removeChild(VisualElement);
			VisualElement._delete();
			VisualElement = null;
			super._delete();
		}
//-----------------------------------------------------------------------------------------------------
// Функции
//-----------------------------------------------------------------------------------------------------
		public function NextFrame():void {
			// Вызывается каждый раз за игровой цикл для подсчета FPS
			RefreshCount++;
			var CurrentTick:uint = getTimer();	// Кол-во мс. с момента запуска
			FramesDiff = CurrentTick - LastTick;
			var FramesDiffRound:uint = CurrentTick - LastTickRound;
			if(FramesDiffRound>=1000) {	// Считаем FPS усредненно за 1 секунду
				var fps:uint = Math.round(RefreshCount*1000/FramesDiffRound);
				VisualElement.Text = "FPS: "+String(fps);
				RefreshCount = 0;
				LastTickRound = CurrentTick;
			}
			LastTick = CurrentTick;
//			trace(FramesDiff+" = "+FramesDiffRound);
//			trace(getTimer());
		}
//-----------------------------------------------------------------------------------------------------
		public function GetFramesDiff():uint {
			// Возвращает кол-во милисекунд за которое была отработана предыдущая итерация цикла
			return FramesDiff;
		}
//-----------------------------------------------------------------------------------------------------
		public function GetCurrentTick():uint {
			// Возвращает кол-во милисекунд с начала работы программы
			return getTimer();
//var a:Date = new Date();
//return a.getTime();	// кол-во миллисекунд с 1 января 1970 года (UTC)
		}
//-----------------------------------------------------------------------------------------------------
// Обработчики событий
//-----------------------------------------------------------------------------------------------------
		
//-----------------------------------------------------------------------------------------------------
	}
}