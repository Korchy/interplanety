package Vn.System {
	// Класс Консоль
//-----------------------------------------------------------------------------------------------------
	import flash.text.TextField;
	import flash.text.TextFieldAutoSize;
	import flash.utils.Timer;
	import flash.events.Event;
	import flash.events.TimerEvent;
	import Vn.Interplanety;
//-----------------------------------------------------------------------------------------------------
	public class Console extends TextField {
//-----------------------------------------------------------------------------------------------------
// Переменные
//-----------------------------------------------------------------------------------------------------
		private var TextTimer:Timer;	// Таймер для убирания текста по времени
//-----------------------------------------------------------------------------------------------------
// Конструктор
//-----------------------------------------------------------------------------------------------------
		public function Console() {
			// Конструктор
			// Таймер
			TextTimer = new Timer(10000);	// Срабатывание раз в 10 секунд
			TextTimer.addEventListener(TimerEvent.TIMER,OnTimer);	// Обработка событя по таймеру
			// Формат текста
			multiline = true;
			autoSize = TextFieldAutoSize.NONE;
			mouseWheelEnabled = true;
			selectable = false;
			height = 150;
			width = 500;
			Resize();	// Установить положение
			border = false;
			borderColor = 0x444444;
			wordWrap = true;
			textColor = 0xff0000;
		}
//-----------------------------------------------------------------------------------------------------
// Деструктор - вызывать самому, автоматически не вызывается
//-----------------------------------------------------------------------------------------------------
		public function _delete():void {
			// Деструктор
			TextTimer.stop();
			TextTimer.removeEventListener(TimerEvent.TIMER,OnTimer);
			TextTimer = null;
		}
//-----------------------------------------------------------------------------------------------------
// Функции
//-----------------------------------------------------------------------------------------------------
		public function Add(Value:String):void {
			if(text=="") text = Value+"\n";
			else {
				// Оставляем 20 строк
				if(numLines>20) {
					var Line2:uint = getLineOffset(1);
					text = text.substring(Line2);
				}
				appendText(Value+"\n");
			}
			// Прокрутка вниз
			scrollV = maxScrollV;
			// Включить таймер для убирания текста
			TextTimer.start();	// Запустить таймер
		}
//-----------------------------------------------------------------------------------------------------
		public function Resize():void {
			// Изменение размеров в зависимости от размеров флешки
			// Меняем только положение
			x = 0;
			y = Vn.Interplanety.Height - height;
		}
//-----------------------------------------------------------------------------------------------------
// Обработка событий
//-----------------------------------------------------------------------------------------------------
		private function OnTimer(e:Event):void {
			// Отработка таймера
			// Убрать верхнюю строку текста
			var Line2:uint = getLineOffset(1);
			text = text.substring(Line2);
			// Весь текст убран - остановить обработку
			if(text=="") TextTimer.stop();
		}
//-----------------------------------------------------------------------------------------------------
// Методы get/set для установки значений переменных
//-----------------------------------------------------------------------------------------------------

//-----------------------------------------------------------------------------------------------------
	}
}